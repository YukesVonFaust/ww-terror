ACTOR Tracer : FastProjectile
{
	+HITTRACER
	RenderStyle Add
	Radius 2
	Height 2
	Speed 75
	+BLOODSPLATTER
	Scale .3
	MissileType TracerTrail
	MissileHeight 8
	Decal BulletChip
	States
	{
	Spawn:
		WPUF B 1 BRIGHT
		Loop
	Death:
	Crash:
		TNT1 A 0 A_CheckFlag("ISMONSTER",2,AAPTR_TRACER)
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_JumpIf(CallACS("NoLevelup")==0,2)
		TNT1 A 0 A_JumpIfHealthLower(1,"Kill",AAPTR_TRACER)
	Kill:
		PUFF A 2 BRIGHT A_SpawnDebris("TracerSpark")
		PUFF B 1 BRIGHT A_SetTranslucent(0.9,0)
		PUFF B 1 BRIGHT A_PlaySound("Bullet/Ricochet",CHAN_BODY,.5)
		PUFF CD 2 BRIGHT
		Stop
	XDeath:
		TNT1 A 0 A_CheckFlag("ISMONSTER",2,AAPTR_TRACER)
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_JumpIf(CallACS("NoLevelup")==0,2)
		TNT1 A 0 A_JumpIfHealthLower(1,"Kill2",AAPTR_TRACER)
	Kill2:
		TNT1 A 3 A_PlaySound("Bullet/HitFlesh",CHAN_BODY,.5)
		Stop
	}
}

ACTOR TracerTrail
{
	RenderStyle Add
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	+CANNOTPUSH
	+NODAMAGETHRUST
	Scale .3
	States
	{
	Spawn:
		WPUF B 3 BRIGHT
		Stop
	}
}

ACTOR TracerSpark
{
	Health 4
	radius 3
	height 6
	speed .1
	RenderStyle ADD
	Scale .4
	Mass 0
	+missile
	+FLOORCLIP
	+DONTSPLASH
	+NOTELEPORT
	BounceType Doom
	States
	{
	Spawn:
		WPUF A 1
		WPUF A 5 Bright A_SetTranslucent(.8,1)
		WPUF B 6 Bright A_SetTranslucent(.6,1)
		WPUF C 8 Bright A_SetTranslucent(.4,1)
		WPUF D 10 Bright A_SetTranslucent(.2,1)
		Stop
	}
}

// Launcher Grenades

ACTOR PlayerLauncherGrenade
{
	+HITTRACER
	Radius 4
	Height 3
	Speed 55
	Damage (1)
	Gravity .2
	DamageType "PlayerMGL"
	Decal Scorch
	PROJECTILE
	-NOGRAVITY
	States
	{
	Spawn:
		SGRN A -1
		Stop
	Death:
		TNT1 A 0 A_CheckFlag("ISMONSTER",2,AAPTR_TRACER)
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_JumpIf(CallACS("NoLevelup")==0,2)
		TNT1 A 0 A_JumpIfHealthLower(1,"Kill",AAPTR_TRACER)
		FRME A 0 A_AlertMonsters
		FRME A 0 A_SetScale(1.75,1.75)
		FRME A 0 A_SetTranslucent(0.75,1)
		FRME A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FRME A 0 A_SetDamageType("PlayerMGLBoom")
		FRME A 1 BRIGHT A_Explode(128,192)
		FRME B 1 BRIGHT A_Quake(7,10,0,256,0)
		FRME CDEFGHIHJKLMNOPQRS 1 bright
		stop
	Kill:
		TNT1 A 0 A_GiveToTarget("MGLExp")
		Goto Death+4
	}
}

ACTOR PlayerAKGrenade : PlayerLauncherGrenade
{
	Damage (100)
	DamageType "PlayerAKGrenade"
	States
	{
	Death:
		FRME A 0 A_AlertMonsters
		FRME A 0 A_SetScale(1.0,1.0)
		FRME A 0 A_SetTranslucent(0.75,1)
		FRME A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FRME A 1 BRIGHT A_Explode(32,192)
		FRME B 1 BRIGHT A_Quake(7,10,0,256,0)
		FRME CDEFGHIHJKLMNOPQRS 1 bright
		stop
	}
}

ACTOR PlayerLauncherGrenadeFaster : FastProjectile
{
	+HITTRACER
	Radius 4
	Height 3
	Speed 75
	Damage (1)
	DamageType "PlayerMGL"
	Decal Scorch
	States
	{
	Spawn:
		SGRN A -1
		Stop
	Death:
		TNT1 A 0 A_CheckFlag("ISMONSTER",2,AAPTR_TRACER)
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_JumpIf(CallACS("NoLevelup")==0,2)
		TNT1 A 0 A_JumpIfHealthLower(1,"Kill",AAPTR_TRACER)
		FRME A 0 A_AlertMonsters
		FRME A 0 A_SetScale(1.75,1.75)
		FRME A 0 A_SetTranslucent(0.75,1)
		FRME A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FRME A 0 A_SetDamageType("PlayerMGLBoom")
		FRME A 1 bright A_Explode(128,192)
		FRME B 1 BRIGHT A_Quake(7,10,0,256,0)
		FRME CDEFGHIHJKLMNOPQRS 1 bright
		stop
	Kill:
		TNT1 A 0 A_GiveToTarget("MGLExp")
		Goto Death+4
	}
}

ACTOR PlayerLauncherGrenadeFaster2 : PlayerLauncherGrenadeFaster
{
	Damage (64)
}

ACTOR PlayerLauncherGrenadeFinal : PlayerLauncherGrenadeFaster2
{
	DamageType Fire
	States
	{
	Death:
		FBLX A 0 A_SetScale(1.0,1.0)
	 	FBLX A 0 A_AlertMonsters
		FBLX A 0 A_SetTranslucent(0.75,1)
		FBLX A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FBLX A 1 BRIGHT A_Explode(128,192)
		FBLX B 1 BRIGHT A_Quake(7,10,0,256,0)
		FBLX CCCCCCCCCC 0 A_CustomMissile("ThermaBurstFireChunk",0,0,(random(0,359)),6,(random(4,15)))
		FBLX CDEFGHIJK 2 BRIGHT
	 	Stop
  	}
}

ACTOR ThermaBurstFireChunk
{
	Damage 6
	Alpha .9
	Radius 4
	Height 4
	Speed 13
	Renderstyle Add
	Obituary "%o is welcomed to %k's barbecue."
	Decal PlasmaScorchLower
	DamageType Fire
	PROJECTILE
	-NOGRAVITY
	+STRIFEDAMAGE
	+NODAMAGETHRUST
	States
	{
	Spawn:
		FFLC ABCDABCDABCDABCD 3 BRIGHT
	Death:
		FFLG ABCD 3 BRIGHT
		FFLG EFGHIJKLMNOPQEFGHIJKLMNOPQEFGHIJKLMNOPQ 2 BRIGHT A_Explode(3,128)
		FFLG RST 3 BRIGHT
		Stop
	}
}

ACTOR ThermaBurstMolotovChunk : ThermaBurstFireChunk
{
	DamageType PlayerGrenadeFire
}

ACTOR EnemyLauncherGrenade : PlayerLauncherGrenade
{
	Speed 30
	Damage (1)
	DamageType "EnemyGrenade"
	+NOGRAVITY
	States
	{
	Death:
		FRME A 0 A_SetScale(1.75,1.75)
		FRME A 0 A_SetTranslucent(0.75,1)
		FRME A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FRME A 1 bright A_Explode(32,256,0)
		FRME B 1 A_Quake(7,10,0,256,0)
		FRME CDEFGHIHJKLMNOPQRS 1 bright
		stop
	}
}

// Player weapon tracers. These contain the damage numbers and speed adjustments

ACTOR PlayerPistolTracer : Tracer
{
	Damage (random(15,20))
	DamageType "PlayerPistol"
	Speed 90
	States
	{
	Kill:
		TNT1 A 0 A_GiveToTarget("BerettaExp")
		Goto Death+4
	Kill2:
		TNT1 A 0 A_GiveToTarget("BerettaExp")
		Goto XDeath+4
	}
}

ACTOR PlayerPistolTracerFaster : PlayerPistolTracer
{
	Damage (random(18,20))
	DamageType "PlayerPistolPierce"
	Speed 100
}

ACTOR PlayerUziTracer : Tracer
{
	Damage (random(16,19))
	DamageType "PlayerUzi"
	Speed 85
	States
	{
	Kill:
		TNT1 A 0 A_GiveToTarget("UziExp")
		Goto Death+4
	Kill2:
		TNT1 A 0 A_GiveToTarget("UziExp")
		Goto XDeath+4
	}
}

ACTOR PlayerAK47Tracer : Tracer
{
	Damage (30)
	DamageType "PlayerAK"
	Speed 120
	States
	{
	Kill:
		TNT1 A 0 A_GiveToTarget("AK47Exp")
		Goto Death+4
	Kill2:
		TNT1 A 0 A_GiveToTarget("AK47Exp")
		Goto XDeath+4
	}
}

ACTOR PlayerAK47TracerFaster : PlayerAK47Tracer
{
	Damage (35)
	Speed 150
}

ACTOR PlayerM60Tracer : Tracer
{
	Damage (random(25,30))
	DamageType "PlayerM60"
	Speed 140
	States
	{
	Kill:
		TNT1 A 0 A_GiveToTarget("M60Exp")
		Goto Death+4
	Kill2:
		TNT1 A 0 A_GiveToTarget("M60Exp")
		Goto XDeath+4
	}
}

ACTOR PlayerM60TracerFaster : PlayerM60Tracer
{
	Damage (random(25,34))
	Speed 160
}

ACTOR PlayerShotgunTracer : Tracer
{
	Damage (random(8,13))
	DamageType "PlayerShotgun"
	Speed 75
	States
	{
	Death:
	Crash:
		TNT1 A 0 A_CheckFlag("ISMONSTER",2,AAPTR_TRACER)
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_JumpIf(CallACS("NoLevelup")==0,2)
		TNT1 A 0 A_JumpIfHealthLower(1,"Kill",AAPTR_TRACER)
		PUFF A 2 BRIGHT A_SpawnDebris("TracerSpark")
		PUFF B 2 BRIGHT A_SetTranslucent(0.9,0)
		PUFF CD 2 BRIGHT
		Stop
	Kill:
		TNT1 A 0 A_GiveToTarget("StrikerExp")
		Goto Death+4
	Kill2:
		TNT1 A 0 A_GiveToTarget("StrikerExp")
		Goto XDeath+4
	}
}

ACTOR PlayerRevolverTracer : Tracer
{
	Damage (random(40,75))
	DamageType "PlayerMagnum"
	Speed 110
	States
	{
	Kill:
		TNT1 A 0 A_GiveToTarget("MatebaExp")
		Goto Death+4
	Kill2:
		TNT1 A 0 A_GiveToTarget("MatebaExp")
		Goto XDeath+4
	}
}

ACTOR PlayerBarrettTracer : Tracer
{
	Damage (130)
	DamageType "Player50BMG"
	Speed 200
	Scale .6
	States
	{
	Kill:
		TNT1 A 0 A_GiveToTarget("BarrettExp")
		Goto Death+4
	Kill2:
		TNT1 A 0 A_GiveToTarget("BarrettExp")
		Goto XDeath+4
	}
}

ACTOR PlayerBarrettTracerLevel2 : PlayerBarrettTracer
{
	Damage (200)
	States
	{
	Death:
	XDeath:
		TNT1 A 0 A_CheckFlag("ISMONSTER",2,AAPTR_TRACER)
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_JumpIf(CallACS("NoLevelup")==0,2)
		TNT1 A 0 A_JumpIfHealthLower(1,"Kill",AAPTR_TRACER)
		FRME A 0 A_SetScale(.4,.4)
		FRME A 0 A_SetTranslucent(.75,1)
		FRME A 0 A_PlaySound("Barrett/Explode")
		FRME A 1 bright A_Explode(64,32,0)
		FRME B 1 bright A_Quake(7,10,0,100,0)
		FRME CDEFGHIHJKLMNOPQRS 1 bright
		stop
	Kill:
	Kill2:
		TNT1 A 0 A_GiveToTarget("BarrettExp")
		Goto Death+4
	}
}

ACTOR PlayerBarrettTracerLevel3 : PlayerBarrettTracer
{
	Damage (200)
	Speed 250
	+RIPPER
	+NOBOSSRIP
}

ACTOR PlayerAR10Tracer : Tracer
{
	Damage (random(20,24))
	DamageType "PlayerAR"
	Speed 150
	States
	{
	Kill:
		TNT1 A 0 A_GiveToTarget("AR10Exp")
		Goto Death+4
	Kill2:
		TNT1 A 0 A_GiveToTarget("AR10Exp")
		Goto XDeath+4
	}
}
ACTOR PlayerAR10TracerSilent : PlayerAR10Tracer
{
	Damage (random(16,18))
	Speed 100
}

// Enemy weapon tracers. In addition to damage numbers and speeds,
// these also must have DamageType "EnemyBullet" so enemies can't kill each other

ACTOR EnemyPistolTracer : Tracer
{
	Damage (random(5,6))
	Speed 50
	FastSpeed 90
	DamageType "EnemyBullet"
	DamageType "Enemy9mm"
}

ACTOR EnemyMagnumTracer : EnemyPistolTracer
{
	Damage (random(9,12))
}

ACTOR EnemyDualiesTracer : EnemyPistolTracer
{
	Damage (random(1,4))
	FastSpeed 100
}

ACTOR EnemySMGTracer : EnemyPistolTracer
{
	Damage (random(3,6))
	Speed 70
	FastSpeed 100
}

ACTOR EnemyARTracer : Tracer
{
	Damage (random(2,10))
	Speed 90
	FastSpeed 120
	DamageType "EnemyBullet"
	DamageType "Enemy762mm"
}

ACTOR EnemyM60Tracer : EnemyARTracer
{
	Damage (random(2,5))
	Speed 55
	FastSpeed 90
}

ACTOR EnemyShotgunTracer : Tracer
{
	Damage (4)
	Speed 40
	FastSpeed 70
	DamageType "EnemyBullet"
	DamageType "Enemy12ga"
	States
	{
	Death:
	Crash:
		PUFF A 2 BRIGHT A_SpawnDebris("TracerSpark")
		PUFF B 2 BRIGHT A_SetTranslucent(0.9,0)
		PUFF CD 2 BRIGHT
		Stop
	}
}

ACTOR WeskerPistolTracer : EnemyARTracer
{
	Damage (random(18,90))
	FastSpeed 90
}

ACTOR CalebPistolTracer : EnemyPistolTracer
{
	Damage (random(6,9))
	Speed 40
	FastSpeed 70
}