// Introducing Alfred Wester, who is totally not Albert Wesker. Honest.

// Voice acting kindly provided by Marty Kirra,
// because I can't afford D.C. Douglas.

ACTOR Wesker : WWTBadGuy replaces Cyberdemon
{
	YScale 0.35
	XScale 0.4
	Health 2000
	Speed 7.5
	PainChance "PlayerPistol",32
	PainChance "PlayerUzi",48
	PainChance "PlayerM60",32
	PainChance "PlayerTaser",40
	PainChance "PlayerGrenadeFire",16
	PainChance "Fire",16
	PainChance "EnemyTaser",40
	Mass 9999
	SeeSound "Sights/Wesker"
	PainSound "Pains/Wesker"
	DeathSound "Deaths/Wesker"
	DamageFactor "EnemyBullet",0.0
	DamageFactor "EnemyGrenade",0.0
	DamageFactor "PlayerGrenadeFire",0.2
	DamageFactor "Fire",0.2
	MeleeThreshold 512
	MeleeRange 96
	Obituary "%o has experienced Complete. Global. Saturation."
	+BOSS
	+MISSILEMORE
	+DONTMORPH
	var int user_taunt;
	States
	{
	Spawn:
		WESK A 1 A_Look
		Loop
	See:
		WESK B 0 A_JumpIf(user_taunt>=40,"Taunt")
		WESK B 0 A_JumpIf(CallACS("IsNightmare") >= 4,"See2")
		WESK B 1 A_Chase("Charge","Missile",CHF_FASTCHASE)
		WESK BBBBB 1 A_Chase("","")
		WESK C 1 A_Chase("Charge","Missile",CHF_FASTCHASE)
		WESK CCCCC 1 A_Chase("","")
		WESK D 1 A_Chase("Charge","Missile",CHF_FASTCHASE)
		WESK DDDDD 1 A_Chase("","")
		WESK E 1 A_Chase("Charge","Missile",CHF_FASTCHASE)
		WESK EEEEE 1 A_Chase("","")
		WESK E 0 A_SetUserVar("user_taunt",user_taunt+1)
		Loop
	See2:
		WESK B 1 A_Chase("Charge","Missile",CHF_FASTCHASE)
		WESK BB 1 A_Chase("","")
		WESK C 1 A_Chase("Charge","Missile",CHF_FASTCHASE)
		WESK CC 1 A_Chase("","")
		WESK D 1 A_Chase("Charge","Missile",CHF_FASTCHASE)
		WESK DD 1 A_Chase("","")
		WESK E 1 A_Chase("Charge","Missile",CHF_FASTCHASE)
		WESK EE 1 A_Chase("","")
		WESK E 0 A_SetUserVar("user_taunt",user_taunt+1)
		Loop
	Missile:
		WESK FGG 3 A_FaceTarget
		WESK G 0 A_PlaySound("Mateba/WeskerFire",CHAN_WEAPON,1.0,0,ATTN_NONE)
		WESK G 2 BRIGHT A_CustomMissile("WeskerPistolTracer",32,0,0)
		WESK G 18 A_FaceTarget
		WESK G 0 A_PlaySound("Mateba/WeskerFire",CHAN_WEAPON,1.0,0,ATTN_NONE)
		WESK G 2 BRIGHT A_CustomMissile("WeskerPistolTracer",32,0,0)
		WESK H 3 A_SetUserVar("user_taunt",0)
		WESK F 4
		Goto See
	Charge:
		WESK F 2 A_FaceTarget
		WESK F 0 A_PlaySound("kick/shockwave")
		WESK F 1 A_Recoil(-80)
		WESK FFFFFFFFF 1 A_SpawnItemEx("RepelShock")
		WESK I 3 A_SetUserVar("user_taunt",0)
		WESK H 5 A_CustomMeleeAttack(45,"skeleton/melee","skeleton/swing","WeskerPunch")
		Goto See
	Taunt:
		WESK A 0 A_SetUserVar("user_taunt",0)
		WESK A 5 A_JumpIfCloser(1500,1)
		Goto See
		WESK A 25 A_PlaySound("Active/Wesker",CHAN_AUTO,1.0,0,ATTN_NONE)
		Goto See
	Pain.PlayerTaser:
		WESK I 0 A_SetUserVar("user_taunt",0)
		Goto Tased
	Pain:
		WESK I 4 A_Pain
		WESK A 2 A_SetUserVar("user_taunt",0)
		Goto See
	Death:
		WESK J 3 A_Scream
		WESK KL 3
		WESK M 3 A_NoBlocking
		WESK N 3 A_BossDeath
		WESK O 3 A_PlaySound("Misc/Bodyfall")
		WESK P -1
		Stop
	}
}