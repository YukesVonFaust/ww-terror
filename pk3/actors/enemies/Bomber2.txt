ACTOR Bomber2 : WWTBadGuy replaces Fatso
{
	YScale 0.35
	XScale 0.4
	Health 250
	Speed 12
	MeleeThreshold 192
	SeeSound "Sights/Male08"
	PainSound "Pains/Male08"
	DeathSound "Deaths/Male08"
	DropItem "GrenadePickup",128
	DropItem "GrenadePickup",64
	DamageFactor "EnemyBullet",0.3
	DamageFactor "EnemyGrenade",0.0
	DamageFactor "PlayerGrenade",0.3
	DamageFactor "PlayerAKGrenade",0.3
	DamageFactor "PlayerMGL",0.3
	Obituary "%o was purged in a baptism of frag grenades."
	States
	{
	Spawn:
		BMB2 A 1 A_Look
		Loop
	See:
		BMB2 BCDE 6 Fast A_Chase
		Loop
	Missile:
		BMB2 F 0 A_PlaySound("Grenade/Throw",CHAN_WEAPON)
		BMB2 AF 8 Fast A_FaceTarget
		BMB2 G 0 A_Jump(64,"Missile2")
		BMB2 G 5 A_CustomMissile("EnemyGrenade",40,0,frandom(-2.0,2.0))
		BMB2 H 5
		Goto See
	Missile2:
		BMB2 F 2 A_PlaySound("Throws/Male08",CHAN_VOICE)
		BMB2 G 2 A_CustomMissile("EnemyGrenade",40,0,frandom(-10.0,-8.0))
		BMB2 F 0 A_PlaySound("Grenade/Throw",CHAN_WEAPON)
		BMB2 H 5 Fast
		BMB2 A 2
		BMB2 F 2 A_PlaySound("Throws/Male08",CHAN_VOICE)
		BMB2 G 2 A_CustomMissile("EnemyGrenade",40,0,frandom(-2.0,2.0))
		BMB2 F 0 A_PlaySound("Grenade/Throw",CHAN_WEAPON)
		BMB2 H 5 Fast
		BMB2 A 2
		BMB2 F 2 A_PlaySound("Throws/Male08",CHAN_VOICE)
		BMB2 G 2 A_CustomMissile("EnemyGrenade",40,0,frandom(8.0,10.0))
		BMB2 F 0 A_PlaySound("Grenade/Throw",CHAN_WEAPON)
		BMB2 H 5 Fast
		BMB2 H 6
		Goto See
	Pain.PlayerElectricGrenade:
	Pain.PlayerTaser:
	Pain.EnemyTaser:
		BMB2 I 0
		Goto Tased
	Pain:
		BMB2 I 3 A_Pain
		Goto See
	Death:
		BMB2 J 3 A_Scream
		BMB2 KL 3
		BMB2 M 3 A_PlaySound("Misc/Bodyfall")
		BMB2 N 3 A_NoBlocking
		BMB2 O 3 A_BossDeath
		BMB2 P 3
		BMB2 Q -1
		Stop
	}
}