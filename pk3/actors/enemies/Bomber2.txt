ACTOR Bomber2 : WWTBadGuy replaces Fatso
{
	YScale 0.35
	XScale 0.4
	Health 250
	MeleeThreshold 192
	SeeSound "Sights/Male08"
	PainSound "Pains/Male08"
	DeathSound "Deaths/Male08"
	DropItem "GrenadePickup",128
	DropItem "GrenadePickup",64
	DropItem "GrenadeUpgradeKit", 24
	DamageFactor "EnemyBullet",0.3
	DamageFactor "EnemyGrenade",0.0
	DamageFactor "PlayerGrenade",0.3
	DamageFactor "PlayerGrenadeBoom",0.3
	DamageFactor "PlayerAKGrenade",0.3
	DamageFactor "PlayerMGL",0.3
	DamageFactor "PlayerMGLBoom",0.3
	Obituary "%o was purged in a baptism of frag grenades."
	States
	{
	Spawn:
		BMB2 A 1 A_LookEx(0,0,0,0,0,"SeeFirst")
		Loop
	SeeFirst:
		BMB2 B 6 Fast A_Chase
		BMB2 B 0 A_JumpIf(CallACS("IsNightmare") >= 4,2)
		BMB2 B 0 A_SetSpeed(2)
		Goto See+8
		BMB2 A 0 A_SetSpeed(4)
		Goto See2+4
	See:
		BMB2 B 0 A_JumpIf(CallACS("IsNightmare") >= 4,"See2")
		BMB2 B 0 A_SetSpeed(2)
		BMB2 B 1 A_Chase
		BMB2 BBBBB 1 A_Chase("","")
		BMB2 C 1 A_Chase
		BMB2 CCCCC 1 A_Chase("","")
		BMB2 D 1 A_Chase
		BMB2 DDDDD 1 A_Chase("","")
		BMB2 E 1 A_Chase
		BMB2 EEEEE 1 A_Chase("","")
		Loop
	See2:
		BMB2 A 0 A_SetSpeed(4)
		BMB2 B 1 A_Chase
		BMB2 BB 1 A_Chase("","")
		BMB2 C 1 A_Chase
		BMB2 CC 1 A_Chase("","")
		BMB2 D 1 A_Chase
		BMB2 DD 1 A_Chase("","")
		BMB2 E 1 A_Chase
		BMB2 EE 1 A_Chase("","")
		Loop
	Missile:
		BMB2 F 0 A_PlaySound("Grenade/Throw",CHAN_WEAPON)
		BMB2 AF 8 Fast A_FaceTarget
		BMB2 G 0 A_Jump(64,"Missile2")
		BMB2 G 5 A_CustomMissile("EnemyGrenade",40,0,frandom(-2.0,2.0))
		BMB2 H 5
		Goto See
	Missile2:
		BMB2 F 2 A_PlaySound("Throws/Male08",CHAN_VOICE)
		BMB2 G 2 A_CustomMissile("EnemyGrenade",40,0,frandom(-10.0,-8.0))
		BMB2 F 0 A_PlaySound("Grenade/Throw",CHAN_WEAPON)
		BMB2 H 5 Fast
		BMB2 A 2
		BMB2 F 2 A_PlaySound("Throws/Male08",CHAN_VOICE)
		BMB2 G 2 A_CustomMissile("EnemyGrenade",40,0,frandom(-2.0,2.0))
		BMB2 F 0 A_PlaySound("Grenade/Throw",CHAN_WEAPON)
		BMB2 H 5 Fast
		BMB2 A 2
		BMB2 F 2 A_PlaySound("Throws/Male08",CHAN_VOICE)
		BMB2 G 2 A_CustomMissile("EnemyGrenade",40,0,frandom(8.0,10.0))
		BMB2 F 0 A_PlaySound("Grenade/Throw",CHAN_WEAPON)
		BMB2 H 5 Fast
		BMB2 H 6
		Goto See
	Pain.PlayerTaser:
		BMB2 I 0
		Goto Tased
	Pain:
		BMB2 I 3 A_Pain
		Goto See
	Death:
		BMB2 J 3 A_Scream
		BMB2 KL 3
		BMB2 M 3 A_PlaySound("Misc/Bodyfall")
		BMB2 N 3 A_NoBlocking
		BMB2 O 3 A_BossDeath
		BMB2 P 3
		BMB2 Q -1
		Stop
	}
}