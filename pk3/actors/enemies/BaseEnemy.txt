ACTOR WWTBadGuy
{
	Height 56
	PainChance 255
	Monster
	+FLOORCLIP
	+BOSSDEATH
	Var Int user_stuntime;
	var int user_spent;
	Speed 12
	States
	{
	SeeFirst:
		"####" B 6 A_Chase
		"####" B 0 A_SetSpeed(2)
		Goto See+7
	See:
		"####" B 0 A_SetSpeed(2)
		"####" B 1 A_Chase
		"####" BBBBB 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCCCC 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DDDDD 1 A_Chase("","")
		"####" E 1 A_Chase
		"####" EEEEE 1 A_Chase("","")
		Loop
	Tased:
		"----" A 0 A_PlaySound("Tasered/Male",CHAN_VOICE)
		"----" A 0 A_ChangeFlag("NoPain",1)
		"----" A 0 A_SetUserVar("user_stuntime",(STUNTIME / 2))
		"----" A 2 BRIGHT
		"----" AAAA 0 A_SpawnItemEx("TaserPuff",8,8,frandom(4,52),0,0,0,random(0,359))
		"----" A 0 A_JumpIf((user_stuntime % 12) != 0,2)
		"----" A 0 A_JumpIf(user_stuntime <= 0,2)
		"----" A 0 A_SetUserVar("user_stuntime",user_stuntime - 1)
		Goto Tased+3
		"----" A 0 A_ChangeFlag("NoPain",0)
		"----" A 0 A_Jump(256,"See")
		Stop
	Pain.EnemyTaser:
		"----" A 0 A_Jump(256,"Pain.PlayerTaser")
		Stop
	Death.PlayerMGLBoom:
		"----" A 0 A_JumpIf(CallACS("NoLevelup")==0,2)
		"----" A 0 A_GiveToTarget("MGLExp")
		"----" A 0 A_Jump(256,"Death")
		Stop
	Death.Player50BMGBoom:
		"----" A 0 A_JumpIf(CallACS("NoLevelup")==0,2)
		"----" A 0 A_GiveToTarget("BarrettExp")
		"----" A 0 A_Jump(256,"Death")
		Stop
	Death.PlayerGrenadeBoom:
	Death.PlayerElectricGrenadeBoom:
		"----" A 0 A_JumpIf(CallACS("NoLevelup")==0,2)
		"----" A 0 A_GiveToTarget("GrenadeExp")
		"----" A 0 A_Jump(256,"Death")
		Stop
	Death.Fire:
		"####" B 0 A_PlaySound("Burning/Male")
		"####" B 0 A_SpawnItemEx("Flame1A",0,0,32)
		"####" B 0 A_Recoil(-8)
		"####" B 0 A_SpawnItemEx("Ember1",0,0,32)
		"####" BBBB 1 A_SetAngle(12 + angle)
		"####" C 0 A_Recoil(-8)
		"####" C 0 A_SpawnItemEx("Flame2A",0,0,32)
		"####" CCCC 1 A_SetAngle(12 + angle)
		"####" D 0 A_Recoil(-8)
		"####" D 0 A_SpawnItemEx("Flame1A",0,0,32)
		"####" DDDD 1 A_SetAngle(12 + angle)
		"####" E 0 A_Recoil(-8)
		"####" EEEE 1 A_SetAngle(12 + angle)
		"####" E 0 A_SpawnItemEx("Flame3B",0,0,32)
		"####" E 0 A_Jump(128,"Death.Fire")
		"####" E 0 A_Jump(256,"Death")
		Stop
	}
}

ACTOR WWTRobot : WWTBadGuy
{
	-FLOORCLIP
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	+DONTRIP
	Health 300
	Radius 31
	Mass 400
	PainSound "Bullet/HitMetal"
	States
	{
	Tased:
		"----" A 0 A_ChangeFlag("NoPain",1)
		"----" A 0 A_Gravity
		"----" A 0 A_SetUserVar("user_stuntime",(STUNTIME / 2))
		"----" A 2 BRIGHT
		"----" A 0 A_JumpIf(user_stuntime <= 0,2)
		"----" A 0 A_SetUserVar("user_stuntime",user_stuntime - 1)
		Goto Tased+3
		"----" A 0 A_NoGravity
		"----" A 0 A_ChangeFlag("NoPain",0)
		"----" A 0 A_Jump(256,"See")
		Stop
	Death.Fire:
		"----" A 0 A_Jump(256,"Death")
		Stop
	}
}