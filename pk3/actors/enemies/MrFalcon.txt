// Mr. Falcon.
// Amazingly hammy voice kindly provided by BouncyTEM.

ACTOR MrFalconReload : Inventory
{
	Inventory.MaxAmount 6
}

ACTOR MrFalcon : WWTBadGuy replaces SpiderMastermind
{
	YScale 0.35
	XScale 0.4
	Health 1000
	Speed 6.66666667
	PainChance "PlayerPistol",32
	PainChance "PlayerUzi",48
	PainChance "PlayerM60",32
	PainChance "PlayerTaser",40
	PainChance "PlayerGrenadeFire",16
	PainChance "Fire",16
	PainChance "EnemyTaser",40
	Mass 9999
	SeeSound "Sights/MrFalcon"
	PainSound "Pains/MrFalcon"
	DeathSound "UberDeaths/MrFalcon"
	DamageFactor "EnemyBullet",0.0
	DamageFactor "EnemyGrenade",0.0
	DamageFactor "PlayerPistol",0.4
	DamageFactor "PlayerUzi",0.4
	DamageFactor "PlayerShotgun",0.4
	DamageFactor "PlayerAK",0.9
	DamageFactor "PlayerAR",0.9
	DamageFactor "PlayerM60",0.9
	DamageFactor "PlayerMagnum",0.7
	DamageFactor "PlayerMelee",2.0
	DamageFactor "PlayerGrenade",0.5
	DamageFactor "PlayerGrenadeBoom",0.5
	DamageFactor "PlayerAKGrenade",0.5
	DamageFactor "PlayerMGL",0.5
	DamageFactor "PlayerMGLBoom",0.5
	DamageFactor "PlayerGrenadeFire",0.2
	DamageFactor "Fire",0.2
	Obituary "%o was yippie-ki-yay'd by Mr. Falcon."
	+BOSS
	+MISSILEMORE
	+MISSILEEVENMORE
	+DONTMORPH
	States
	{
	Spawn:
		LTHG A 1 A_Look
		Loop
	See:
		LTHG B 1 A_Chase
		LTHG BB 1 A_Chase("","")
		LTHG B 1 A_Chase
		LTHG BB 1 A_Chase("","")
		LTHG C 1 A_Chase
		LTHG CC 1 A_Chase("","")
		LTHG C 1 A_Chase
		LTHG CC 1 A_Chase("","")
		LTHG D 1 A_Chase
		LTHG DD 1 A_Chase("","")
		LTHG D 1 A_Chase
		LTHG DD 1 A_Chase("","")
		LTHG E 1 A_Chase
		LTHG EE 1 A_Chase("","")
		LTHG E 1 A_Chase
		LTHG EE 1 A_Chase("","")
		Loop
	Missile:
		LTHG F 0 A_JumpIfInventory("MrFalconReload",6,"Reload")
		LTHG F 0 A_PlaySound("Firing/MrFalcon",CHAN_AUTO)
		LTHG F 8 Fast A_PlaySound("MGL/Close",CHAN_AUTO,1.0,0,0.4)
		LTHG G 8 Fast A_FaceTarget
		LTHG G 0 A_PlaySound("MGL/Fire",CHAN_AUTO,1.0,0,0.4)
		LTHG G 2 BRIGHT A_CustomMissile("EnemyLauncherGrenade",46,0,frandom(-3.0,3.0),CMF_OFFSETPITCH,frandom(-3.0,3.0))
		LTHG G 10 A_GiveInventory("MrFalconReload")
		Goto See
	Reload:
		LTHG F 0 A_PlaySound("Reloading/MrFalcon",CHAN_VOICE)
		LTHG F 10 A_PlaySound("MGL/Open",CHAN_WEAPON)
	Reloading:
		LTHG G 0 A_TakeInventory("MrFalconReload",1)
		LTHG G 7 Fast A_PlaySound("MGL/Load",CHAN_WEAPON)
		LTHG F 5
		LTHG F 0 A_JumpIfInventory("MrFalconReload",1,"Reloading")
		LTHG A 5 A_PlaySound("MGL/Close",CHAN_WEAPON)
		Goto See
	Pain.PlayerTaser:
		LTHG H 0
		Goto Tased
	Pain:
		LTHG H 0 A_JumpIfHealthLower(200,2)
		LTHG H 3 A_Pain
		Goto See
		LTHG H 3 A_PlaySound("HeavyPain/MrFalcon",CHAN_VOICE)
		Goto See
	Death:
		LTHG H 20 A_Scream
		LTHG HHHHHHHHHHHHHHHHHHHHIJKLMN 3 A_SpawnItemEx("DIExplosion",frandom(-20.0,20.0),frandom(-20.0,20.0),frandom(0,32.0),0,0,0,SXF_NOCHECKPOSITION)
		LTHG O 3 A_PlaySound("Misc/Bodyfall")
		LTHG P 3
		LTHG P 0 A_NoBlocking
		LTHG P -1 A_BossDeath
		Stop
	}
}