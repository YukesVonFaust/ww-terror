// Ammos
ACTOR 9mmAmmo : Ammo
{
	Inventory.Amount 8
	Inventory.MaxAmount 150
	Ammo.BackpackAmount 15
	Ammo.BackpackMaxAmount 300
	Inventory.Icon "9MMPB0"
	Scale .15
	Inventory.PickupMessage "Picked up some 9mm ammo."
	Inventory.PickupSound "Beretta/Ready"
	Tag "9x19mm"
	States
	{
	Spawn:
		9MMP B -1
		Stop
	}
}

ACTOR 9mmAmmoSmall : 9mmAmmo
{
	Inventory.Amount 6
	Inventory.PickupMessage "Picked up a bit of 9mm ammo."
	Inventory.PickupSound "Beretta/Drop"
	States
	{
	Spawn:
		9MMP A -1
		Stop
	}
}

ACTOR 9mmAmmoLarge : 9mmAmmo replaces Shell
{
	Inventory.Amount 20
	Inventory.PickupMessage "Picked up a box of 9mm ammo."
	Inventory.PickupSound "Uzi/Clipout"
	Scale .2
	States
	{
	Spawn:
		9MMP C -1
		Stop
	}
}

ACTOR 12gaAmmo : Ammo
{
	Inventory.Amount 12
	Inventory.MaxAmount 48
	Ammo.BackpackAmount 6
	Ammo.BackpackMaxAmount 96
	Inventory.Icon "12GPA0"
	Scale .25
	Inventory.PickupMessage "Picked up a box of 12-gauge ammo."
	Inventory.PickupSound "Striker/Load"
	Tag "12g Shell"
	States
	{
	Spawn:
		12GP A -1
		Stop
	}
}

ACTOR 12gaAmmoSmall : 12gaAmmo
{
	Inventory.Amount 5
	Inventory.PickupMessage "Picked up a handful of 12-gauge shells."
	Inventory.PickupSound "Striker/Eject"
	States
	{
	Spawn:
		12GP B -1
		Stop
	}
}

ACTOR 762mmAmmo : Ammo replaces ShellBox
{
	Inventory.Amount 20
	Inventory.MaxAmount 200
	Ammo.BackpackAmount 15
	Ammo.BackpackMaxAmount 400
	Inventory.Icon "762PA0"
	Scale .15
	Inventory.PickupMessage "Picked up some 7.62mm ammo."
	Inventory.PickupSound "AK47/Clipout"
	Tag "7.62mm"
	States
	{
	Spawn:
		762P A -1
		Stop
	}
}

ACTOR 762mmAmmoLarge : 762mmAmmo replaces CellPack
{
	Inventory.Amount 90
	Inventory.PickupMessage "Picked up a box of 7.62mm ammo."
	Inventory.PickupSound "M60/Load"
	Scale .4
	States
	{
	Spawn:
		762P B -1
		Stop
	}
}

ACTOR 44MagAmmo : Ammo
{
	Inventory.Amount 6
	Inventory.MaxAmount 42
	Ammo.BackpackAmount 4
	Ammo.BackpackMaxAmount 84
	Inventory.Icon "44MPB0"
	Scale .2
	Inventory.PickupMessage "Picked up a box of .44 Magnum ammo."
	Inventory.PickupSound "Mateba/Open"
	Tag ".44 Magnum"
	States
	{
	Spawn:
		44MP B -1
		Stop
	}
}

ACTOR 44MagAmmoSmall : 44MagAmmo
{
	Inventory.Amount 2
	Inventory.PickupMessage "Picked up a used .44 Magnum cylinder."
	Inventory.PickupSound "Mateba/Out"
	Scale .13
	States
	{
	Spawn:
		44MP A -1
		Stop
	}
}

ACTOR 50BMGAmmo : Ammo replaces RocketBox
{
	Inventory.Amount 5
	Inventory.MaxAmount 25
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 50
	Inventory.Icon "AMRAA0"
	Scale .2
	Inventory.PickupMessage "Picked up some .50 BMG ammo."
	Inventory.PickupSound "Barrett/Clipout"
	Tag ".50 BMG"
	States
	{
	Spawn:
		AMRA A -1
		Stop
	}
}

ACTOR 40mmAmmo : Ammo replaces Cell
{
	Inventory.Amount 6
	Inventory.MaxAmount 30
	Ammo.BackpackAmount 3
	Ammo.BackpackMaxAmount 60
	Inventory.Icon "AMRAA0"
	Scale .3
	Inventory.PickupMessage "Picked up some 40mm HE grenades."
	Inventory.PickupSound "MGL/Load"
	Tag "40mm HE Grenade"
	States
	{
	Spawn:
		40MP A -1
		Stop
	}
}

Actor WWTBackpack : BackpackItem
{
	Height 26
	Inventory.PickupMessage "Picked up an ammo pack."
	Inventory.PickupSound "Pickups/Backpack"
	Scale .25
	States
	{
	Spawn:
		BPK2 A -1
		Stop
	}
}

ACTOR BackpackSpawner replaces Backpack
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("WWTBackpack")
		TNT1 AA 0 A_SpawnItemEx("GrenadePickup",frandom(-16,16),frandom(-16,16),0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR BulletSpawner1 : RandomSpawner replaces Clip
{
	DropItem "9mmAmmo"
	DropItem "9mmAmmo"
	DropItem "44MagAmmoSmall"
}

ACTOR BulletSpawner2 : RandomSpawner replaces ClipBox
{
	DropItem "12gaAmmo"
	DropItem "44MagAmmo"
}

// Armors
ACTOR WWTArmorBonus : ArmorBonus replaces ArmorBonus
{
	Scale .2
	Inventory.PickupMessage "Picked up an armor pad."
	Inventory.PickupSound "Pickups/Armor1"
	States
	{
	Spawn:
		RMR1 A -1
		Stop
	}
}

ACTOR WWTKevlarArmor : BasicArmorPickup replaces GreenArmor
{
	Inventory.PickupMessage "Picked up a kevlar recon vest."
	Inventory.PickupSound "Pickups/Armor2"
	Armor.SaveAmount 100
	Armor.SavePercent 75
	DamageFactor "Enemy9mm",.5
	DamageFactor "EnemyShotgun",.2
	DamageFactor "Enemy762mm",1.0
	Scale .4
	States
	{
	Spawn:
		RMR2 A 10 Bright
		RMR2 A 10
		Loop
	}
}

ACTOR WWTCompositeArmor : BasicArmorPickup replaces BlueArmor
{
	Inventory.PickupMessage "Picked up a composite suit."
	Inventory.PickupSound "Pickups/Armor3"
	Armor.SaveAmount 150
	Armor.SavePercent 50
	DamageFactor "Enemy9mm",.2
	DamageFactor "EnemyShotgun",.2
	DamageFactor "Enemy762mm",.4
	Scale .5
	States
	{
	Spawn:
		RMR3 A 10 Bright
		RMR3 A 10
		Loop
	}
}

ACTOR SupershieldArmor : BasicArmorPickup
{
	Armor.SaveAmount 250
	Armor.SavePercent 100
}

ACTOR SupershieldArmor2x : SupershieldArmor
{
	Armor.SaveAmount 500
	Armor.SavePercent 100
}

ACTOR SupershieldInvulnEffect : PowerInvulnerable
{
	Powerup.Duration -15
}

ACTOR SupershieldInvulnItem : PowerupGiver
{
	+COUNTITEM
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.BIGPOWERUP
	Inventory.MaxAmount 0
	Powerup.Type "SupershieldInvulnEffect"
	Powerup.Color BlueMap
}

ACTOR SupershieldItem : CustomInventory
{
	Inventory.PickupMessage "Found an unrefined Isotope-556 plant."
	Inventory.PickupSound "Pickups/Plant"
	Scale .2
	States
	{
	Spawn:
		HERB A 10 Bright
		HERB A 10
		Loop
	Pickup:
		VIAL A 0 A_GiveInventory("SupershieldArmor")
		VIAL A 0 A_GiveInventory("SupershieldInvulnItem")
		Stop
	}
}

ACTOR SuperSupershieldItem : SupershieldItem
{
	Inventory.PickupMessage "Received a vial of refined Isotope-556."
	Inventory.PickupSound "Pickups/Vial"
	States
	{
	Spawn:
		VIAL A 10 Bright
		VIAL A 10
		Loop
	Pickup:
		VIAL A 0 A_GiveInventory("SupershieldArmor2x")
		Goto Super::Pickup+1
	}
}

// Health
ACTOR PowerBooze : Health
{
	+COUNTITEM
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	Inventory.Amount 100
	Inventory.MaxAmount 200
	Inventory.PickupMessage "Received some strong booze."
	Inventory.PickupSound "Pickups/Booze"
	Scale .5
	States
	{
	Spawn:
		BOOZ A 10
		BOOZ A 10 Bright
		Loop
	}
}

ACTOR WeakBooze : PowerBooze
{
	Inventory.Amount 25
	Inventory.PickupMessage "Found some cheap booze."
	Scale .6
	States
	{
	Spawn:
		BOOZ B 10
		BOOZ B 10 Bright
		Loop
	}
}

ACTOR WWTBandage : HealthBonus replaces HealthBonus
{
	Scale .075
	Inventory.PickupMessage "Picked up a MediPatch."
	Inventory.PickupSound "Pickups/MedSmall"
	States
	{
	Spawn:
		BAND A -1
		Stop
	}
}

ACTOR WWTGauze : Stimpack replaces Stimpack
{
	Scale .1
	Inventory.PickupMessage "Picked up a HealFast(tm) bandage."
	Inventory.PickupSound "Pickups/MedMedium"
	States
	{
	Spawn:
		GAUZ A -1
		Stop
	}
}

ACTOR WWTMedkit : Medikit replaces Medikit
{
	Scale .08
	Inventory.PickupMessage "Picked up a medical kit."
	Inventory.PickupSound "Pickups/MedLarge"
	Health.LowMessage 25,"Picked up a badly needed medical kit."
	States
	{
	Spawn:
		FAID A -1
		Stop
	}
}

// Others
ACTOR IbexUpgradeKit : CustomInventory
{
	Inventory.PickupMessage "Received an IBEX Upgrade Kit."
	Inventory.PickupSound "Pickups/Ibex"
	Scale .4
	States
	{
	Spawn:
		IBEX A 10 BRIGHT
		IBEX A 10
		Loop
	Pickup:
		IBEX A 0 ACS_NamedExecuteAlways("InstantLevelup")
		Stop
	}
}

ACTOR SuperBattery : CustomInventory
{
	Inventory.PickupMessage "Found a Thunderbolt battery. (Taser upgrade!)"
	Inventory.PickupSound "Taser/Upgrade"
	Scale .25
	States
	{
	Spawn:
		9VLT A 10 BRIGHT
		9VLT A 10
		Loop
	Pickup:
		9VLT A 0 A_GiveInventory("TaserExp",1000)
		Stop
	}
}

ACTOR GrenadeUpgradeKit : CustomInventory
{
	Inventory.PickupMessage "Found a pile of grenade parts. (Grenade upgrade!)"
	Inventory.PickupSound "Grenade/Upgrade"
	Scale .25
	States
	{
	Spawn:
		JNK9 A 10 BRIGHT
		JNK9 A 10
		Loop
	Pickup:
		JNK9 A 0 A_GiveInventory("GrenadeExp",1000)
		Stop
	}
}

ACTOR WeakBlueprint : MapRevealer
{
	+COUNTITEM
	+INVENTORY.FANCYPICKUPSOUND
	+INVENTORY.ALWAYSPICKUP
	Inventory.MaxAmount 0
	Inventory.PickupSound "Pickups/Scroll"
	Inventory.PickupMessage "Found the blueprints for this area."
	States
	{
	Spawn:
		BMAP A 10 Bright
		BMAP A 10
		Loop
	}
}

ACTOR StrongBlueprint : CustomInventory
{
	+COUNTITEM
	+INVENTORY.FANCYPICKUPSOUND
	+INVENTORY.ALWAYSPICKUP
	Inventory.MaxAmount 0
	Inventory.PickupSound "Pickups/Scroll"
	Inventory.PickupMessage "Received vital intelligence for this area."
	States
	{
	Spawn:
		BMAP B 10 Bright
		BMAP B 10
		Loop
	Pickup:
		BMAP B 0 A_GiveInventory("WeakBlueprint")
		BMAP B 0 A_GiveInventory("WWTScanner")
		Stop
	}
}

ACTOR WWTScanner : PowerupGiver
{
	Inventory.MaxAmount 1
	Powerup.Duration 0x7fffffff
	Powerup.Type "Scanner"
	+INVENTORY.AUTOACTIVATE
}

ACTOR WeakKickLevel : Medikit
{
	Inventory.PickupMessage "Found some painkillers."
	Health.LowMessage 25,"Painkillers. Geez, could get addicted at this rate..."
	Inventory.PickupSound "Pickups/Pills"
	Inventory.Amount 100
	Scale .6
	States
	{
	Spawn:
		PILL A 10 BRIGHT
		PILL A 10
		Loop
	}
}

ACTOR StrongKickLevel : CustomInventory
{
	Inventory.PickupMessage "Received spiritual enlightenment."
	Inventory.PickupSound "Pickups/Scroll"
	Scale .5
	States
	{
	Spawn:
		SCRL A 10 BRIGHT
		SCRL A 10
		Loop
	Pickup:
		SCRL A 0 A_GiveInventory("KickExp",1000)
		Stop
	}
}

ACTOR StrongKoin : CustomInventory
{
	Scale .1
	+COUNTITEM
	+INVENTORY.FANCYPICKUPSOUND
	+INVENTORY.ALWAYSPICKUP
	Inventory.MaxAmount 0
	Inventory.PickupSound "Pickups/KombatKoin"
	Inventory.PickupMessage "Found a Koin...how'd that get here?"
	States
	{
	Spawn:
		KOIN A 10 Bright
		KOIN A 10
		Loop
	Pickup:
		KOIN A 0 A_GiveInventory("PowerBooze")
		KOIN A 0 A_GiveInventory("WWTCompositeArmor",150)
		Stop
	}
}