ACTOR BulletCasing
{
	Projectile
	-NOGRAVITY
	-NOBLOCKMAP
	+BOUNCEONACTORS
	BounceType Doom
	BounceSound "bulletcasing/bounce"
	BounceFactor .25
	WallBounceFactor .1
	Mass 5
	Radius 3
	Height 3
	Scale .1
	States
	{
	Spawn:
		BCAS ABCDEF 2
		Loop
	Death:
		"####" A 0 A_Jump(256,1,2,3,4,5,6)
		"####" ABCDEF 0 A_Jump(256,"Death1")
		Stop
	Death1:
		"####" "#" 1 A_SetTics(CallACS("ShellLifetime"))
		"####" "#" 1 A_FadeOut(0.05)
		Wait
	}
}

ACTOR MagnumCasing : BulletCasing
{
	Scale .2
}

ACTOR 40mmCasing : BulletCasing
{
	Scale .6
	States
	{
	Spawn:
		CAS6 ABCDEF 2
		Loop
	}
}

ACTOR RifleCasing : MagnumCasing
{
	States
	{
	Spawn:
		RCAS ABCDEF 2
		Loop
	}
}

ACTOR M60Spacer : BulletCasing
{
	Scale .25
	States
	{
	Spawn:
		SPCR ABCD 2
		Loop
	Death:
		SPCR A 0 A_Jump(256,1,2,3,4)
		SPCR ABCD 0 A_Jump(256,"Death1")
		Stop
	}
}

ACTOR BarrettCasing : RifleCasing
{
	Scale .4
}

ACTOR ShellCasing : BulletCasing
{
	WallBounceSound "shellcasing/bounce"
	BounceSound "shellcasing/bounce"
	DeathSound "shellcasing/stop"
	BounceFactor .5
	WallBounceFactor 1
	Scale .4
	States
	{
	Spawn:
		SHLL ABCDEFGHIJKLMNOP 1
		Loop
	Death:
		SHLL A 0 A_Jump(256,1,2)
		SHLL CK 0 A_Jump(256,"Death1")
		Stop
	}
}

ACTOR SpentClipPistol : BulletCasing	//Pistol Clip
{
	Scale .75
	Radius 3
	Height 3
	Mass 5
	+DROPOFF
	+MISSILE
	+NOTELEPORT
	+FLOORCLIP
	DeathSound "weapons/smallclip"
	States
	{
	Spawn:
		CLP1 C -1
		Stop
	Death:
		CLP1 I 0
	Death1:
		"####" "#" 1 A_SetTics(CallACS("ShellLifetime"))
		"####" "#" 1 A_FadeOut(0.05)
		Wait
	}
}

ACTOR SpentClipRifle : SpentClipPistol	//Skorpion Clip
{
	Scale .5
	DeathSound "weapons/largeclip"
	States
	{
	Spawn:
		CLP2 ABCD 3
		Loop
	Death:
		CLP2 E 0
		Goto Death1
	}
}

ACTOR SpentClipBigRifle : SpentClipRifle	//Slugthrower clip
{
	Scale 1
	States
	{
	Spawn:
		CLP3 ABCD 3
		Loop
	Death:
		CLP3 A 0
		Goto Death1
	}
}

ACTOR SpentClipSMG : SpentClipRifle	//SMG clip
{
	Scale .125
	States
	{
	Spawn:
		CLP4 ABCDEF 3
		Loop
	Death:
		CLP4 A 0
		Goto Death1
	}
}

ACTOR SpentClipMagnum : SpentClipPistol	//Magnum Speedloader
{
	Scale .35
	States
	{
	Spawn:
		SLOD B -1
		Stop
	Death:
		SLOD B 0
		Goto Death1
	}
}