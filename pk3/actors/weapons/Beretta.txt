// Original sprite: Prime Target by MacSoft.
// Edit: Mike12.
// Hand: MidoriMan (with contributions by Minigunner)
// Muzzle Flash: ???
// Edits and Pickup: wildweasel.
// Sounds: Daikatana and Deus Ex by Ion Storm.

ACTOR BerettaExp : Inventory
{
	Inventory.MaxAmount 25
}

ACTOR BerettaLevelup : Inventory
{
	Inventory.MaxAmount 3
}

ACTOR BerettaMaxed : Inventory
{
	Inventory.MaxAmount 1
}

ACTOR BerettaLoaded : Ammo
{
	Inventory.MaxAmount 27
	Inventory.Icon "BRTPA0"
	+IGNORESKILL
}

ACTOR BerettaBurstActive : Inventory
{
	Inventory.MaxAmount 1
}

ACTOR BerettaBurstSeq : Inventory
{
	Inventory.MaxAmount 3
}

ACTOR Beretta : WWTWeapon replaces Pistol
{
	Weapon.AmmoType1 "BerettaLoaded"
	Weapon.AmmoGive1 0
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "9mmAmmo"
	Weapon.AmmoGive2 5
	Weapon.AmmoUse2 0
	Weapon.SlotNumber 1
	Radius 16
	Height 16
	Scale 0.5
	Inventory.PickupMessage "$WWTWEP1PICK"
	Inventory.PickupSound "Beretta/Ready"
	Inventory.Icon "BRTPA0"
	+Weapon.NoAutoFire
	+Weapon.Ammo_Optional
	+Weapon.NoAlert
	Tag "$WWTWEP1NAME"
	States
	{
	Spawn:
		BRTP A -1
		Stop
	Deselect:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory("BerettaBurstSeq", 3)
		BRR2 I 1 Offset(20,35)
		BRR2 I 1 Offset(40,45)
		BRR2 I 1 Offset(70,65)
		BRR2 I 1 Offset(100,80)
	DeselectLoop:
		BRTG A 0 A_Lower
		Wait
	Select:
	Readyanim:
		TNT1 A 3
		BRR2 K 1 Offset(40,50)
		BRR2 L 1 Offset(30,40)
		BRR2 M 1 Offset(20,35)
		BRR2 N 1 Offset(10,34) A_PlaySound("Beretta/Ready",5)
		BRR2 N 1 Offset(4,34)
		BRR2 O 1 Offset(2,33)
		BRR2 O 1 Offset(1,33)
		BRTR P 1
		BRTR Q 1 Offset(4,35)
		BRTR Q 1 Offset(9,37)
		BRTR R 1 Offset(6,36)
		BRTR S 1 Offset(2,33)
		BRTG A 1 Offset(9,50)
		BRTG A 1 Offset(3,41)
		BRTG A 1 Offset(1,33)
	Ready:
		BRTG A 0 A_JumpIfInventory("BerettaBurstActive", 1, "ReadyBurst")
		BRTG A 0 A_JumpIfInventory("BerettaLoaded", 1, 1)
		Goto ReadySlideback
		BRTG A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Loop
	ReadySlideback:
		BRTG E 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Goto Ready
	ReadyBurst:
		BRTB A 0 A_JumpIfInventory("BerettaLoaded", 1, 1)
		Goto ReadyBurstSlideback
		BRTB A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Goto Ready
	ReadyBurstSlideback:
		BRTB E 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Goto Ready
	Zoom:
		BRTG A 0 A_JumpIfInventory("BerettaLevelUp", 3, "ToggleBurst")
		BRTG A 1
		Goto Ready
	ToggleBurst:
		BRTG A 0 A_JumpIfInventory("BerettaBurstActive", 1, "UnToggleBurst")
		BRTG A 0 ACS_NamedExecute("PistolToggleMessage")
	BurstOn:
		BRTG A 0 A_GiveInventory("BerettaBurstActive", 1)
		BRTG A 0 A_JumpIfNoAmmo("BurstOnAnimEmpty")
		BRTG A 1 Offset(-1,33)
		BRTG A 1 Offset(-2,34)
		BRTB G 1 Offset(-3,35)
		BRTB G 1 Offset(-2,36)
	BurstOnQuick:
		BRTB G 1 Offset(-1,35)
		BRTB A 1 Offset(1,34)
		BRTB A 1 Offset(1,33)
		BRTB AAAAAAAAAA 1 A_WeaponReady
		Goto ReadyBurst
	BurstOnAnimEmpty:
		BRTG E 1 Offset(-1,33)
		BRTG E 1 Offset(-2,34)
		BRTB H 1 Offset(-3,35)
		BRTB H 1 Offset(-2,36)
		BRTB H 1 Offset(-1,35)
		BRTB E 1 Offset(1,34)
		BRTB E 1 Offset(1,33)
		BRTB EEEEEEEEEE 1 A_WeaponReady
		Goto Ready
	UnToggleBurst:
		BRTG A 0 A_TakeInventory("BerettaBurstActive", 1)
		BRTG A 0 ACS_NamedExecute("PistolToggleMessage")
		BRTG A 0 A_JumpIfNoAmmo("BurstOffAnimEmpty")
		BRTB A 1 Offset(-1,33)
		BRTB A 1 Offset(-2,34)
		BRTB G 1 Offset(-3,35)
		BRTB G 1 Offset(-2,36)
		BRTB G 1 Offset(-1,35)
		BRTG A 1 Offset(1,34)
		BRTG A 1 Offset(1,33)
		BRTG A 5
		Goto Ready
	BurstOffAnimEmpty:
		BRTB E 1 Offset(-1,33)
		BRTB E 1 Offset(-2,34)
		BRTB H 1 Offset(-3,35)
		BRTB H 1 Offset(-2,36)
		BRTB H 1 Offset(-1,35)
		BRTG E 1 Offset(1,34)
		BRTG E 1 Offset(1,33)
		BRTG E 5
		Goto Ready
	Fire:
		BRTG A 0 A_JumpIfInventory("BerettaBurstActive", 1, "FireLevel3")
		BRTG A 0 A_JumpIfNoAmmo("Dryfire")
		BRTG A 0 A_JumpIfInventory("BerettaLevelup", 2, "FireLevel2")
		BRTG A 0 A_JumpIfInventory("BerettaLevelup", 1, "FireLevel1")
		BRTF B 0 A_PlayWeaponSound("Beretta/PlayerFire")
		BRTF A 0 A_GunFlash
		BRTF A 0 A_AlertMonsters
		BRTF A 1 Offset(2,34) BRIGHT A_FireCustomMissile("PlayerPistolTracer", frandom(-1.2, 1.2), 1, 8, 0, 0, frandom(-1.2, 1.2))
		BRTF A 0 A_SetPitch(pitch - 0.4)
		BRTG B 1 Offset(5,37) A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		BRTG C 1 Offset(6,38) A_SetPitch(pitch - 0.4)
		BRTG A 0 A_JumpIfNoAmmo("FireSlideback")
		BRTG D 1 Offset(8,39)
		BRTG A 1 Offset(9,40)
		BRTG A 1 Offset(8,39)
		BRTG A 1 Offset(6,36) A_WeaponReady(WRF_NOBOB)
		BRTG A 1 Offset(3,34) A_WeaponReady(WRF_NOBOB)
		BRTG A 1 Offset(1,33) A_WeaponReady(WRF_NOBOB)
		BRTG A 1 Offset(-1,31) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	FireLevel1:
		BRTF B 0 A_PlayWeaponSound("Beretta/PlayerFire")
		BRTF A 0 A_GunFlash
		BRTF A 0 A_AlertMonsters
		BRTF A 1 Offset(2,34) BRIGHT A_FireCustomMissile("PlayerPistolTracer", frandom(-1.2, 1.2), 1, 8, 0, 0, frandom(-1.2, 1.2))
		BRTF A 0 A_SetPitch(pitch - 0.4)
		BRTG B 1 Offset(5,37) A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		BRTG C 1 Offset(6,38) A_SetPitch(pitch - 0.4)
		BRTG A 0 A_JumpIfNoAmmo("FireSlideback")
		BRTG D 1 Offset(8,39) A_WeaponReady(WRF_NOBOB)
		BRTG A 1 Offset(9,40) A_WeaponReady(WRF_NOBOB)
		BRTG A 1 Offset(8,39) A_WeaponReady(WRF_NOBOB)
		BRTG A 1 Offset(6,36) A_WeaponReady(WRF_NOBOB)
		BRTG A 1 Offset(3,34) A_WeaponReady(WRF_NOBOB)
		BRTG A 1 Offset(1,33) A_WeaponReady(WRF_NOBOB)
		BRTG A 1 Offset(-1,31) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	FireLevel2:
		BRTF A 0 A_PlayWeaponSound("Beretta/PlayerFire")
		BRTF A 0 A_GunFlash
		BRTF A 0 A_AlertMonsters
		BRTF A 1 Offset(2,34) BRIGHT A_FireCustomMissile("PlayerPistolTracerFaster", frandom(-0.5, 0.5), 1, 8, 0, 0, frandom(-0.5, 0.5))
		BRTF A 0 A_SetPitch(pitch - 0.2)
		BRTG B 1 Offset(5,37) A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		BRTG C 1 Offset(6,38) A_SetPitch(pitch - 0.2)
		BRTG A 0 A_JumpIfNoAmmo("FireSlideback")
		BRTG D 1 Offset(8,39) A_WeaponReady(WRF_NOBOB)
		BRTG A 1 Offset(9,40) A_WeaponReady(WRF_NOBOB)
		BRTG A 1 Offset(8,39) A_WeaponReady(WRF_NOBOB)
		BRTG A 1 Offset(6,36) A_WeaponReady(WRF_NOBOB)
		BRTG A 1 Offset(3,34) A_WeaponReady(WRF_NOBOB)
		BRTG A 1 Offset(1,33) A_WeaponReady(WRF_NOBOB)
		BRTG A 1 Offset(-1,31) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	FireLevel3:
		BRTB A 0 A_JumpIfNoAmmo("DryfireBurst")
		BRTF B 0 A_JumpIfInventory("BerettaBurstSeq", 3, "FireLevel3Done")
		BRTF B 0 A_PlayWeaponSound("Beretta/PlayerFire")
		BRTF B 0 A_GunFlash
		BRTF B 0 A_AlertMonsters
		BRTF B 0 A_GiveInventory("BerettaBurstSeq", 1)
		BRTF B 1 Offset(2,34) BRIGHT A_FireCustomMissile("PlayerPistolTracerFaster", frandom(-0.5, 0.5), 1, 8, 0, 0, frandom(-0.5, 0.5))
		BRTF B 0 A_SetPitch(pitch - 0.2)
		BRTB B 1 Offset(5,37) A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		BRTB C 0 Offset(6,38) A_SetPitch(pitch - 0.2)
		BRTB A 0 A_JumpIfNoAmmo("FireSlidebackBurst")
		BRTB C 1 A_Refire
		BRTB A 0 A_TakeInventory("BerettaBurstSeq", 3)
		BRTB D 1 Offset(8,39) A_WeaponReady(WRF_NOBOB)
		BRTB A 1 Offset(9,40) A_WeaponReady(WRF_NOBOB)
		BRTB A 1 Offset(8,39) A_WeaponReady(WRF_NOBOB)
		BRTB A 1 Offset(6,36) A_WeaponReady(WRF_NOBOB)
		BRTB A 1 Offset(3,34) A_WeaponReady(WRF_NOBOB)
		BRTB A 1 Offset(1,33) A_WeaponReady(WRF_NOBOB)
		BRTB A 1 Offset(-1,31) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	FireLevel3Done:
		BRTB D 1 Offset(8,39) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		BRTB A 1 Offset(9,40) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		BRTB A 1 Offset(8,39) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		BRTB A 1 Offset(6,36) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		BRTB A 1 Offset(3,34) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		BRTB A 1 Offset(1,33) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		BRTB A 1 Offset(-1,31) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		BRTB A 1 A_TakeInventory("BerettaBurstSeq",3)
		Goto Ready
	FireSlideback:
		BRTG E 1 Offset(8,39) A_TakeInventory("BerettaBurstSeq", 3)
		BRTG E 1 Offset(9,40)
		BRTG E 1 Offset(8,39)
		BRTG E 1 Offset(6,36)
		BRTG E 1 Offset(3,34)
		BRTG E 1 Offset(1,33)
		BRTG E 1 Offset(-1,31)
		Goto ReadySlideback
	FireSlidebackBurst:
		BRTB E 1 Offset(8,39) A_TakeInventory("BerettaBurstSeq", 3)
		BRTB E 1 Offset(9,40)
		BRTB E 1 Offset(8,39)
		BRTB E 1 Offset(6,36)
		BRTB E 1 Offset(3,34)
		BRTB E 1 Offset(1,33)
		BRTB E 1 Offset(-1,31)
		Goto ReadyBurstSlideback
	Dryfire:
		BRTG F 1 A_PlaySound("Dryfire/Pistol",6)
		BRTG F 1 Offset(2,34)
		BRTG E 1 Offset(1,33)
		Goto Ready
	DryfireBurst:
		BRTB F 1 A_PlaySound("Dryfire/Pistol",6)
		BRTB F 1 Offset(2,34)
		BRTB E 1 Offset(1,33)
		Goto Ready
	Reload:
		BRTG A 0 A_JumpIfInventory("BerettaMaxed", 1, "ReloadBigger")
		BRTG A 0 A_JumpIfInventory("BerettaLoaded", 15, 3)
		BRTG A 0 A_JumpIfInventory("9mmAmmo", 1, "ReloadBegin")
		BRTG A 0 A_JumpIfInventory("BerettaBurstActive", 1, "ReloadFailBurst")
		BRTG A 0 A_JumpIfInventory("BerettaLoaded", 1, 1)
		Goto ReloadFailEmpty
		BRTG A 1
		Goto Ready
	ReloadBigger:
		BRTG A 0 A_JumpIfInventory("BerettaLoaded", 27, 3)
		BRTG A 0 A_JumpIfInventory("9mmAmmo", 1, "ReloadBegin")
		BRTG A 0 A_JumpIfInventory("BerettaBurstActive", 1, "ReloadFailBurst")
		BRTG A 0 A_JumpIfInventory("BerettaLoaded", 1, 1)
		Goto ReloadFailEmpty
		BRTG A 1
		Goto Ready
	ReloadFailEmpty:
		BRTG E 1
		Goto ReadySlideback
	ReloadFailBurst:
		BRTB A 0 A_JumpIfNoAmmo("ReloadFailBurstEmpty")
		BRTB A 1
		Goto Ready
	ReloadFailBurstEmpty:
		BRTB E 1
		Goto Ready
	ReloadBegin:
		BRTG A 0 A_JumpIfInventory("BerettaLoaded", 1, 1)
		Goto ReloadBeginEmpty
		BRTG A 1 A_PlaySound("Beretta/Reload",5)
		BRTG A 1
		Goto ReloadProper
	ReloadProper:
		BRR2 ABCD 1
		BRR2 D 1 Offset(2,30) A_FireCustomMissile("SpentClipPistol",3,0,5,0)
		BRR2 D 1 Offset(4,28)
		BRR2 D 1 Offset(5,27)
		BRR2 D 1 Offset(4,29)
		BRR2 D 1 Offset(3,31) A_PlaySound("Beretta/Reload2",5)
		BRR2 D 1 Offset(1,32)
		BRR2 D 1 Offset(-1,33)
		BRR2 EFGH 1
		BRR2 H 0 A_JumpIfInventory("BerettaMaxed", 1, "ReloadLoopBigger")
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("9mmAmmo",1)
		TNT1 A 0 A_GiveInventory("BerettaLoaded",1)
		TNT1 A 0 A_JumpIfInventory("BerettaLoaded",15,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("9mmAmmo",1,"ReloadLoop")
		Goto ReloadFinish
	ReloadLoopBigger:
		TNT1 A 0 A_TakeInventory("9mmAmmo",1)
		TNT1 A 0 A_GiveInventory("BerettaLoaded",1)
		TNT1 A 0 A_JumpIfInventory("BerettaLoaded",27,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("9mmAmmo",1,"ReloadLoopBigger")
		Goto ReloadFinish
	ReloadFinish:
		BRR2 I 1
		BRR2 J 1
		BRR2 J 1 Offset(-2,35)
		BRR2 J 1 Offset(-3,38)
		BRR2 J 1 Offset(-4,43)
		BRR2 J 1 Offset(-1,44)
		BRR2 J 1 Offset(2,42)
		BRR2 J 1 Offset(5,40)
		BRR2 J 1 Offset(8,37)
		BRR2 J 1 Offset(11,33)
		BRTG A 1 Offset(12,50)
		BRTG A 1 Offset(11,51)
		BRTG A 1 Offset(10,50)
		BRTG A 1 Offset(8,48)
		BRTG A 1 Offset(6,45)
		BRTG A 1 Offset(3,41)
		BRTG A 1 Offset(2,35)
		BRTG A 1 Offset(1,33)
		BRTG A 1 Offset(-1,31)
		BRTG A 0 A_JumpIfInventory("BerettaBurstActive", 1, "BurstOnQuick")
		Goto Ready
	ReloadBeginEmpty:
		BRTG E 1 A_PlaySound("Beretta/Reload",5)
		BRTG E 1
		Goto ReloadProperEmpty
	ReloadProperEmpty:
		BRTR ABCD 1
		BRTR D 1 Offset(2,30) A_FireCustomMissile("SpentClipPistol",3,0,5,0)
		BRTR D 1 Offset(4,28)
		BRTR D 1 Offset(5,27)
		BRTR D 1 Offset(4,29)
		BRTR D 1 Offset(3,31) A_PlaySound("Beretta/Reload2",5)
		BRTR D 1 Offset(1,32)
		BRTR D 1 Offset(-1,33)
		BRTR EFGH 1
		BRTR H 0 A_JumpIfInventory("BerettaMaxed", 1, "ReloadLoopEmptyBigger")
	ReloadLoopEmpty:
		TNT1 A 0 A_TakeInventory("9mmAmmo",1)
		TNT1 A 0 A_GiveInventory("BerettaLoaded",1)
		TNT1 A 0 A_JumpIfInventory("BerettaLoaded",15,"ReloadFinishEmpty")
		TNT1 A 0 A_JumpIfInventory("9mmAmmo",1,"ReloadLoopEmpty")
		Goto ReloadFinishEmpty
	ReloadLoopEmptyBigger:
		TNT1 A 0 A_TakeInventory("9mmAmmo",1)
		TNT1 A 0 A_GiveInventory("BerettaLoaded",1)
		TNT1 A 0 A_JumpIfInventory("BerettaLoaded",27,"ReloadFinishEmpty")
		TNT1 A 0 A_JumpIfInventory("9mmAmmo",1,"ReloadLoopEmptyBigger")
		Goto ReloadFinishEmpty
	ReloadFinishEmpty:
		BRTR I 1
		BRTR J 1
		BRTR J 1 Offset(-2,35)
		BRTR J 1 Offset(-3,38)
		BRTR J 1 Offset(-4,43)
		BRTR J 1 Offset(-5,44)
		BRTR J 1 Offset(-4,42)
		BRTR J 1 Offset(-3,40)
		BRTR J 1 Offset(-1,37)
		BRTR J 1 Offset(-1,33)
		BRTR KLM 1
		BRTR N 2 A_PlaySound("Beretta/Ready",5)
		BRTR OP 2
		BRTR Q 1
		BRTR Q 1 Offset(4,35)
		BRTR Q 1 Offset(9,37)
		BRTR R 1 Offset(6,36)
		BRTR S 1 Offset(2,33)
		BRTG A 1 Offset(9,50)
		BRTG A 1 Offset(8,48)
		BRTG A 1 Offset(6,45)
		BRTG A 1 Offset(3,41)
		BRTG A 1 Offset(2,35)
		BRTG A 1 Offset(1,33)
		BRTG A 1 Offset(-1,31)
		BRTG A 0 A_JumpIfInventory("BerettaBurstActive", 1, "BurstOnQuick")
		Goto Ready
	AltfireReturn:
		BRTG A 0 A_JumpIfInventory("BerettaBurstActive", 1, "AltfireReturnBurst")
		BRTG A 0 A_JumpIfInventory("BerettaLoaded", 1, 1)
		Goto AltfireReturnSlideback
	AltfireReturnResume:
		BRTG "#" 1 Offset(-30,82)
		BRTG "#" 1 Offset(-25,72)
		BRTG "#" 1 Offset(-20,62)
		BRTG "#" 1 Offset(-15,52)
		BRTG "#" 1 Offset(-10,44)
		BRTG "#" 1 Offset(-5,38)
		BRTG "#" 1 Offset(-1,35)
		Goto Ready
	AltfireReturnSlideback:
		BRTG E 0
		Goto AltfireReturnResume
	AltfireReturnBurst:
		BRTB A 0 A_JumpIfInventory("BerettaLoaded", 1, 1)
		Goto AltfireReturnBurstSlideback
	AltfireReturnBurstResume:
		BRTB "#" 1 Offset(-30,82)
		BRTB "#" 1 Offset(-25,72)
		BRTB "#" 1 Offset(-20,62)
		BRTB "#" 1 Offset(-15,52)
		BRTB "#" 1 Offset(-10,44)
		BRTB "#" 1 Offset(-5,38)
		BRTB "#" 1 Offset(-1,35)
		Goto Ready
	AltfireReturnBurstSlideback:
		BRTB E 0
		Goto AltfireReturnBurstResume
	Flash:
		TNT1 A 3 A_Light2
		TNT1 A 3 A_Light1
		TNT1 A 0 A_Light0
		Goto LightDone
	}
}