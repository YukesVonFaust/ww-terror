ACTOR GrenadeExp : Inventory
{
	Inventory.MaxAmount 25
}

ACTOR GrenadeLevelup : Inventory
{
	Inventory.MaxAmount 3
}

ACTOR GrenadeMaxed : Inventory
{
	Inventory.MaxAmount 1
}

ACTOR GrenadePickup : CustomInventory replaces RocketAmmo
{
	inventory.pickupmessage "Picked up a grenade."
	Tag "Grenade"
	inventory.maxamount 9
	inventory.icon THRWA0
	inventory.usesound "grenade/throw"
	inventory.pickupsound "grenade/pickup"
	+Inventory.invbar
	+inventory.keepdepleted
	Inventory.InterHubamount 9
	Radius 16
	Height 8
	Scale 0.25
	-INVBAR
	states
	{
	Spawn:
		THRW A -1
		stop
	Use:
		TNT1 A 0 A_JumpIfInventory("GrenadeLevelup", 3, "Use3")
		TNT1 A 0 A_JumpIfInventory("GrenadeLevelup", 2, "Use2")
		TNT1 A 0 A_JumpIfInventory("GrenadeLevelup", 1, "Use1")
		TNT1 A 0 A_FireCustomMissile("PipebombGrenade", 0, 0)
		Stop
	Use1:
		TNT1 A 0 A_FireCustomMissile("HandGrenade", 0, 0)
		stop
	Use2:
		TNT1 A 0 A_FireCustomMissile("ElectricGrenade", 0, 0)
		Stop
	Use3:
		TNT1 A 0 A_FireCustomMissile("IncendiaryGrenade", 0, 0)
		Stop
	}
}

ACTOR PipebombGrenade
{
	Radius 4
	Height 3
	Speed 40
	Scale 0.25
	Damage (1)
	DamageType "PlayerGrenade"
	+NOBLOCKMAP 
	+DROPOFF 
	+MISSILE 
	+ACTIVATEIMPACT 
	+ACTIVATEPCROSS
	+CANBOUNCEWATER
	obituary "%o went down the pipes, thanks to %k's pipebomb."
	BounceType Doom
	BounceFactor 0.4
	WallBounceFactor 0.4
	bouncesound "grenade/bounce"
	states
	{
	Spawn:
		PIPE A 2 Bright A_Countdown
		PIPE BCD 2 Bright
		loop
	Death:
		PIPE A 35 Bright
		FRME A 0 A_AlertMonsters
		FRME A 0 A_SetScale(1.75,1.75)
		FRME A 0 A_SetTranslucent(0.75,1)
		FRME A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FRME A 1 bright A_Explode(150,256,1)
		FRME B 1 Bright A_Quake(7,10,0,256,0)
		FRME CDEFGHIHJKLMNOPQRS 1 bright
		stop
	}
}

ACTOR HandGrenade
{
	Radius 4
	Height 3
	Speed 30
	Scale 0.25
	Damage (1)
	Gravity 0.75
	DamageType "PlayerGrenade"
	+NOBLOCKMAP 
	+DROPOFF 
	+MISSILE 
	+ACTIVATEIMPACT 
	+ACTIVATEPCROSS
	+CANBOUNCEWATER
	obituary "%o failed to field %k's fragmenting fastball."
	BounceType Doom
	BounceFactor 0.4
	WallBounceFactor 0.4
	bouncesound "grenade/bounce"
	states
	{
	Spawn:
		THRW A 2 Bright A_Countdown
		THRW BCDEF 2 Bright
		loop
	Death:
		THRW B 35 Bright
		FRME A 0 A_AlertMonsters
		FRME A 0 A_SetScale(1.75,1.75)
		FRME A 0 A_SetTranslucent(0.75,1)
		FRME A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FRME A 1 bright A_Explode(175,256,1,1,128,15,2)
		FRME B 1 Bright A_Quake(7,10,0,256,0)
		FRME CDEFGHIHJKLMNOPQRS 1 bright
		stop
	}
}

ACTOR ElectricGrenade
{
	Radius 4
	Height 3
	Speed 30
	Scale 0.55
	Damage (1)
	Gravity 0.75
	DamageType "PlayerElectricGrenade"
	+NOBLOCKMAP 
	+DROPOFF 
	+MISSILE 
	+ACTIVATEIMPACT 
	+ACTIVATEPCROSS
	+CANBOUNCEWATER
	obituary "%o would make a good conductor, says %k."
	BounceType Doom
	BounceFactor 0.4
	WallBounceFactor 0.4
	bouncesound "grenade/bounce"
	states
	{
	Spawn:
		EGRN A 2 Bright A_Countdown
		EGRN BCDEFGH 2 Bright
		loop
	Death:
		EGRN C 35 Bright
		STFL A 0 A_AlertMonsters
		STFL A 0 A_SetTranslucent(0.75,1)
		STFL A 0 A_PlaySound("grenade/elecexplode",0,1.0,0,0.1)
		STFL A 0 Bright A_Quake(7,10,0,512,0)
		STFL A 1 Bright A_Explode(8,192)
		STFL B 1 Bright A_SetScale(0.6)
		STFL C 1 Bright A_Explode(8,200)
		STFL D 1 Bright A_SetScale(0.7)
		STFL E 1 Bright A_Explode(8,208)
		STFL F 1 Bright A_SetScale(0.8)
		STFL G 1 Bright A_Explode(8,216)
		STFL H 1 Bright A_SetScale(0.9)
		STFL I 1 Bright A_Explode(8,224)
		STFL J 1 Bright A_SetScale(1.0)
		STFL K 1 Bright A_Explode(8,232)
		STFL L 1 Bright A_SetScale(1.2)
		STFL M 1 Bright A_Explode(8,240)
		STFL N 1 Bright A_SetScale(1.4)
		STFL O 1 Bright A_Explode(8,248)
		STFL P 1 Bright A_SetScale(1.6)
		STFL A 1 Bright A_Explode(8,256)
		STFL B 1 Bright A_SetScale(1.8)
		STFL C 1 Bright A_Explode(8,264)
		STFL D 1 Bright A_SetScale(2.0)
		STFL E 1 Bright A_Explode(8,272)
		STFL F 1 Bright A_SetScale(2.25)
		STFL G 1 Bright A_Explode(8,280)
		STFL H 1 Bright A_SetScale(2.5)
		STFL I 1 Bright A_Explode(8,288)
		STFL J 1 Bright A_SetScale(2.75)
		STFL K 1 Bright A_Explode(8,296)
		STFL L 1 Bright A_SetScale(3.0)
		STFL M 1 Bright A_Explode(8,304)
		STFL N 1 Bright A_SetScale(3.25)
		STFL O 1 Bright A_Explode(8,312)
		STFL P 1 Bright A_SetScale(3.5)
		stop
	}
}

ACTOR IncendiaryGrenade
{
	Radius 4
	Height 3
	Speed 40
	Scale 0.25
	Damage (1)
	DamageType "PlayerGrenadeFire"
	+NOBLOCKMAP 
	+DROPOFF 
	+MISSILE 
	+ACTIVATEIMPACT 
	+ACTIVATEPCROSS
	+CANBOUNCEWATER
	obituary "%o was roasted, toasted, and burnt to a crisp by %k."
	BounceType Doom
	BounceFactor 0.4
	WallBounceFactor 0.4
	bouncesound "grenade/bounce"
	states
	{
	Spawn:
		GCAN A 1 Bright A_Countdown
		GCAN BCDEFGH 1 Bright
		loop
	Death:
		GCAN D 35 Bright
		FBLX A 0 BRIGHT A_SetScale(1.0,1.0)
     	FBLX A 0 BRIGHT A_AlertMonsters
		FBLX A 0 BRIGHT A_SetTranslucent(0.75,1)
		FBLX A 0 BRIGHT A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FBLX A 1 BRIGHT A_Explode(128,192,1)
		FBLX B 1 BRIGHT A_Quake(7,10,0,256,0)
		FBLX CCCCCCCCCC 0 BRIGHT A_CustomMissile("ThermaBurstMolotovChunk",0,0,(random(0,359)),6,(random(4,15)))
		FBLX CDEFGHIJK 2 BRIGHT
     	Stop
	}
}

ACTOR EnemyGrenade : HandGrenade
{
	DamageType "EnemyGrenade"
	States
	{
	Death:
		THRW B 35 Bright
		FRME A 0 A_AlertMonsters
		FRME A 0 A_SetScale(1.75,1.75)
		FRME A 0 A_SetTranslucent(0.75,1)
		FRME A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FRME A 1 bright A_Explode(40,128)
		FRME B 1 Bright A_Quake(7,10,0,256,0)
		FRME CDEFGHIHJKLMNOPQRS 1 bright
		stop
	}
}

ACTOR EnemyPipebomb : PipebombGrenade
{
	DamageType "EnemyGrenade"
	States
	{
	Death:
		PIPE A 35 Bright
		FRME A 0 A_AlertMonsters
		FRME A 0 A_SetScale(1.75,1.75)
		FRME A 0 A_SetTranslucent(0.75,1)
		FRME A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FRME A 1 bright A_Explode(40,128)
		FRME B 1 Bright A_Quake(7,10,0,256,0)
		FRME CDEFGHIHJKLMNOPQRS 1 bright
		stop
	}
}