// Berlinghoff AMR-82
// Model credits: r2pp2r, twinke masta, Hav0c, Jennifer!!, Vunsunta, kenlau

ACTOR BarrettExp : Inventory
{
	Inventory.MaxAmount 50
}

ACTOR BarrettLevelup : Inventory
{
	Inventory.MaxAmount 3
}

ACTOR BarrettMaxed : Inventory
{
	Inventory.MaxAmount 1
}

ACTOR BarrettLoaded : Ammo
{
	Inventory.MaxAmount 5
	+IGNORESKILL
}

ACTOR BarrettZoom : Inventory
{
	Inventory.MaxAmount 1
}

ACTOR Barrett : WWTWeapon replaces RocketLauncher
{
	Weapon.SlotNumber 6
	Weapon.AmmoType1 "BarrettLoaded"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 0
	Weapon.AmmoType2 "50BMGAmmo"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 5
	Scale 0.5
	Inventory.Icon "AMRPA0"
	Inventory.PickupMessage "$WWTWEP6PICK"
	Inventory.PickupSound "Barrett/Boltfwd"
	Tag "$WWTWEP6NAME"
	+Weapon.NoAutoFire
	+Weapon.NoAlert
	+Weapon.Ammo_Optional
	States
	{
	Spawn:
		AMRP A -1
		Stop
	Select:
	ReadyAnim:
		TNT1 A 15
		AMRG A 1 Offset(90,91)
		"####" A 1 Offset(75,81)
		"####" A 1 Offset(60,71)
		"####" A 1 Offset(48,61)
		"####" A 1 Offset(38,51)
		"####" A 1 Offset(29,43)
		"####" A 1 Offset(20,36)
		"####" A 1 Offset(11,35)
		Goto ReadyAnim2
	Ready:
		AMRG A 0 A_JumpIfInventory("BarrettZoom", 1, "ReadyZoom")
		AMRG A 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_ALLOWRELOAD)
		Loop
	ReadyZoom:
		SCO2 A 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_ALLOWRELOAD)
		Goto Ready
	Deselect:
		"####" A 0 A_StopSound(CHAN_WEAPON)
		"####" A 0 A_TakeInventory("StrikerLoading", 1)
		"####" A 1 Offset(3,32)
		"####" A 1 Offset(6,33)
		"####" A 1 Offset(11,35)
		"####" A 1 Offset(20,36)
		"####" A 1 Offset(29,43)
		"####" A 1 Offset(38,51)
		"####" A 1 Offset(48,61)
		"####" A 1 Offset(60,71)
		"####" A 1 Offset(75,81)
		"####" A 1 Offset(90,91)
		TNT1 A 0 A_Lower
		Wait
	Fire:
		AMRG A 0 A_JumpIfInventory("BarrettZoom", 1, "FireZoom")
		AMRG A 0 A_JumpIfNoAmmo("Dryfire")
		AMRF A 0 A_PlayWeaponSound("Barrett/Fire")
		AMRF A 0 A_GunFlash
		AMRF A 0 A_AlertMonsters
		AMRF A 0 A_JumpIfInventory("BarrettLevelup", 3, "FireLevel3")
		AMRF A 0 A_JumpIfInventory("BarrettLevelup", 2, "FireLevel2")
		AMRF A 0 A_JumpIfInventory("BarrettLevelup", 1, "FireLevel1")
		AMRF A 1 Bright Offset(3,35) A_FireCustomMissile("PlayerBarrettTracer", frandom(-0.7, 0.7), 1, 9, 0, 0, frandom(-0.6, 0.6))
		AMRF B 0 A_SpawnItemEx("BarrettCasing",24,-32,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		AMRF B 1 Bright Offset(6,38) A_SetPitch(pitch - 1.5)
		AMRG A 1 Bright Offset(9,42) A_SetPitch(pitch - 1.2)
		AMRG A 1 Bright Offset(12,46) A_SetPitch(pitch - 0.9)
		AMRG A 1 Offset(13,48) A_SetPitch(pitch - 0.6)
		AMRG A 1 Offset(12,45) A_SetPitch(pitch - 0.3)
		AMRG A 1 Offset(10,42) A_SetPitch(pitch + 0.1)
		AMRG A 1 Offset(8,39) A_SetPitch(pitch + 0.2)
		AMRG A 1 Offset(6,36) A_SetPitch(pitch + 0.3)
		AMRG A 1 Offset(3,34) A_SetPitch(pitch + 0.4)
		AMRG A 1 Offset(1,33) A_SetPitch(pitch + 0.5)
		AMRG A 1 Offset(-1,31) A_SetPitch(pitch + 0.3)
		AMRG A 1 Offset(-1,32) A_SetPitch(pitch + 0.1)
		Goto Ready
	FireLevel1:
		AMRF A 1 Bright Offset(3,35) A_FireCustomMissile("PlayerBarrettTracer", frandom(-0.7, 0.7), 1, 9, 0, 0, frandom(-0.6, 0.6))
	FireContinue:
		AMRF B 0 A_SpawnItemEx("BarrettCasing",24,-32,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		AMRF B 1 Bright Offset(6,38) A_SetPitch(pitch - 1.0)
		AMRG A 1 Bright Offset(9,42) A_SetPitch(pitch - 0.8)
		AMRG A 1 Bright Offset(12,46) A_SetPitch(pitch - 0.6)
		AMRG A 1 Offset(13,48) A_SetPitch(pitch - 0.4)
		AMRG A 1 Offset(12,45) A_SetPitch(pitch - 0.3)
		AMRG A 1 Offset(10,42) A_SetPitch(pitch + 0.1)
		AMRG A 1 Offset(8,39) A_SetPitch(pitch + 0.2)
		AMRG A 1 Offset(6,36) A_SetPitch(pitch + 0.3)
		AMRG A 1 Offset(3,34) A_SetPitch(pitch + 0.4)
		AMRG A 1 Offset(1,33) A_SetPitch(pitch + 0.5)
		AMRG A 1 Offset(-1,31) A_SetPitch(pitch + 0.3)
		AMRG A 1 Offset(-1,32) A_SetPitch(pitch + 0.1)
		Goto Ready
	FireLevel2:
		AMRF A 1 Bright Offset(3,35) A_FireCustomMissile("PlayerBarrettTracerLevel2", frandom(-0.7, 0.7), 1, 9, 0, 0, frandom(-0.6, 0.6))
		Goto FireContinue
	FireLevel3:
		AMRF A 1 Bright Offset(3,35) A_FireCustomMissile("PlayerBarrettTracerLevel3", frandom(-0.7, 0.7), 1, 9, 0, 0, frandom(-0.6, 0.6))
		Goto FireContinue
	Dryfire:
		AMRG A 1 A_PlaySound("Dryfire/Heavy",6)
		AMRG A 1 Offset(2,34)
		AMRG A 1 Offset(1,33)
		Goto Ready
	FireZoom:
		SCO2 A 0 A_JumpIfNoAmmo("DryfireZoom")
		SCO2 A 0 A_PlayWeaponSound("Barrett/Fire")
		SCO2 A 0 A_GunFlash
		SCO2 A 0 A_AlertMonsters
		SCO2 A 0 A_SetBlend("ff ff aa", 0.5, 10)
		SCO2 A 0 A_JumpIfInventory("BarrettLevelup", 3, "FireZoomedLevel3")
		SCO2 A 0 A_JumpIfInventory("BarrettLevelup", 2, "FireZoomedLevel2")
		SCO2 A 0 A_JumpIfInventory("BarrettLevelup", 1, "FireZoomedLevel1")
		SCO2 A 1 Bright Offset(3,35) A_FireCustomMissile("PlayerBarrettTracer", 0, 1)
		SCO2 A 0 A_SpawnItemEx("RifleCasing",24,-32,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		SCO2 A 1 Bright Offset(6,35) A_SetPitch(pitch - 1.5)
		SCO2 A 1 Bright Offset(9,39) A_SetPitch(pitch - 1.2)
		SCO2 A 1 Bright Offset(12,39) A_SetPitch(pitch - 0.9)
		SCO2 A 1 Offset(13,38) A_SetPitch(pitch - 0.6)
		SCO2 A 1 Offset(12,37) A_SetPitch(pitch - 0.3)
		SCO2 A 1 Offset(10,36) A_SetPitch(pitch + 0.1)
		SCO2 A 1 Offset(8,35) A_SetPitch(pitch + 0.2)
		SCO2 A 1 Offset(6,34) A_SetPitch(pitch + 0.3)
		SCO2 A 1 Offset(3,33) A_SetPitch(pitch + 0.4)
		SCO2 A 1 Offset(1,32) A_SetPitch(pitch + 0.5)
		SCO2 A 1 Offset(-1,31) A_SetPitch(pitch + 0.3)
		SCO2 A 1 Offset(-1,32) A_SetPitch(pitch + 0.1)
		Goto Ready
	FireZoomedLevel1:
		SCO2 A 1 Bright Offset(3,35) A_FireCustomMissile("PlayerBarrettTracer", 0, 1)
	FireZoomedContinue:
		SCO2 A 0 A_SpawnItemEx("RifleCasing",24,-32,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		SCO2 A 1 Bright Offset(6,36) A_SetPitch(pitch - 1.0)
		SCO2 A 1 Bright Offset(9,39) A_SetPitch(pitch - 0.8)
		SCO2 A 1 Bright Offset(12,38) A_SetPitch(pitch - 0.6)
		SCO2 A 1 Offset(13,38) A_SetPitch(pitch - 0.4)
		SCO2 A 1 Offset(12,37) A_SetPitch(pitch - 0.2)
		SCO2 A 1 Offset(10,37) A_SetPitch(pitch + 0.1)
		SCO2 A 1 Offset(8,35) A_SetPitch(pitch + 0.2)
		SCO2 A 1 Offset(6,35) A_SetPitch(pitch + 0.3)
		SCO2 A 1 Offset(3,34) A_SetPitch(pitch + 0.4)
		SCO2 A 1 Offset(1,33) A_SetPitch(pitch + 0.5)
		SCO2 A 1 Offset(-1,31) A_SetPitch(pitch + 0.3)
		SCO2 A 1 Offset(-1,32) A_SetPitch(pitch + 0.1)
		Goto Ready
	FireZoomedLevel2:
		SCO2 A 1 Bright Offset(3,35) A_FireCustomMissile("PlayerBarrettTracerLevel2", 0, 1)
		Goto FireZoomedContinue
	FireZoomedLevel3:
		SCO2 A 1 Bright Offset(3,35) A_FireCustomMissile("PlayerBarrettTracerLevel3", 0, 1)
		Goto FireZoomedContinue
	DryfireZoom:
		SCO2 A 1 A_PlayWeaponSound("Dryfire/Rifle")
		Goto Ready
	Zoom:
		AMRG A 0 A_JumpIfInventory("BarrettZoom", 1, "UnZoom")
		AMRG A 1 A_GiveInventory("BarrettZoom", 1)
		SCO2 A 0 A_PlaySound("Barrett/ZoomIn", 5)
		SCO2 A 0 A_ZoomFactor(3.0)
		SCO2 A 10 A_SetBlend("00 00 00", 1.0, 20)
		SCO2 AAAAAAAAAA 1
		Goto ReadyZoom
	UnZoom:
		SCO2 A 1 A_TakeInventory("BarrettZoom", 1)
		SCO2 A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
		AMRG A 10 A_SetBlend("00 00 00", 1.0, 20)
		AMRG AAAAAAAAAA 1
		Goto Ready
	Reload:
		SCO2 A 0 A_JumpIfInventory("BarrettZoom", 1, "ReloadUnzoom")
	ReloadContinue:
		AMRG A 0 A_JumpIfInventory("BarrettLoaded", 5, 2)
		AMRG A 0 A_JumpIfInventory("50BMGAmmo", 1, 2)
		AMRG A 1
		Goto Ready
		AMRG A 1 Offset(-1,35) A_PlaySound("Barrett/Clipout", 5)
		AMRG A 1 Offset(-2,38)
		AMRG A 1 Offset(-3,42) A_FireCustomMissile("SpentClipBigRifle",3,0,5,0)
		AMRR A 1 Offset(-4,47)
		AMRR A 1 Offset(-5,53)
		AMRR A 1 Offset(-6,56)
		AMRR A 1 Offset(-7,64)
		AMRR A 1 Offset(-8,70)
		AMRR A 1 Offset(-8,71)
		AMRR A 1 Offset(-8,72)
		AMRR A 1 Offset(-9,73)
		AMRR A 1 Offset(-9,74)
		AMRR A 1 Offset(-9,75)
		AMRR A 1 Offset(-10,76)
		AMRR A 1 Offset(-11,75)
		AMRR A 1 Offset(-11,74)
		AMRR A 1 Offset(-10,73)
		AMRR A 1 Offset(-10,72)
		AMRR A 1 Offset(-9,71)
		AMRR A 1 Offset(-9,70)
		AMRR A 1 Offset(-8,69)
		AMRR A 1 Offset(-7,68)
		AMRR A 1 Offset(-7,66)
		AMRR A 1 Offset(-6,64)
		AMRR A 1 Offset(-5,62)
	ReloadWork:
		TNT1 A 0 A_TakeInventory("50BMGAmmo", 1)
		TNT1 A 0 A_GiveInventory("BarrettLoaded", 1)
		TNT1 A 0 A_JumpIfInventory("BarrettLoaded", 5, "ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("50BMGAmmo", 1, "ReloadWork")
		Goto ReloadFinish
	ReloadFinish:
		AMRR A 1 Offset(-4,60) A_PlaySound("Barrett/ClipIn", 5)
		AMRR A 1 Offset(-3,59)
		AMRR A 1 Offset(-2,57)
		AMRG A 1 Offset(-1,55)
		AMRG A 1 Offset(1,53)
		AMRG A 1 Offset(1,52)
		AMRG A 1 Offset(2,51)
		AMRG A 1 Offset(3,50)
		AMRG A 1 Offset(4,47)
		AMRG A 1 Offset(5,45)
		AMRG A 1 Offset(6,43)
		AMRG A 1 Offset(7,40)
		AMRG A 1 Offset(8,35)
	ReadyAnim2:
		AMRG A 1 Offset(9,37)
		AMRR B 1 Offset(10,38)
		AMRR C 1 Offset(11,39)
		AMRR C 1 Offset(12,40)
		AMRR D 1 Offset(14,42) A_PlaySound("Barrett/BoltBack", 5)
		AMRR E 1 Offset(17,44)
		AMRR F 1 Offset(21,46)
		AMRR G 1 Offset(26,48)
		AMRR H 1 Offset(33,50)
		AMRR H 1 Offset(36,51)
		AMRR H 1 Offset(38,52)
		AMRR H 1 Offset(39,53) A_PlaySound("Barrett/BoltFwd", 5)
		AMRR H 1 Offset(37,53)
		AMRR H 1 Offset(35,52)
		AMRR H 1 Offset(34,51)
		AMRR H 1 Offset(32,50)
		AMRR H 1 Offset(30,48)
		AMRR I 1 Offset(23,44)
		AMRR J 1 Offset(15,38)
		AMRR K 1 Offset(4,35)
		AMRG A 1 Offset(-1,31)
		Goto Ready
	ReloadUnzoom:
		SCO2 A 1 A_TakeInventory("BarrettZoom", 1)
		TNT1 A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
		AMRG A 10 A_SetBlend("00 00 00", 1.0, 20)
		Goto ReloadContinue
	Altfire:
		SCO2 A 0 A_JumpIfInventory("BarrettZoom", 1, "AltfireUnzoom")
	AltfireContinue:
		AMRG A 0 A_JumpIfInventory("KickSeq", 1, "KickLevel1")
		AMRG A 1 Offset(-10,35)
		AMRG A 1 Offset(-15,38)
		AMRG A 1 Offset(-35,44)
		AMRG A 1 Offset(-60,52)
		AMRG A 1 Offset(-90,62)
		AMRG A 1 Offset(-130,72)
		AMRG A 1 Offset(-160,82)
		TNT1 A 0 A_JumpIfInventory("WWTTaserActivated", 1, "Taser")
		Goto Super::Kick
	AltfireUnzoom:
		SCO2 A 1 A_TakeInventory("BarrettZoom", 1)
		TNT1 A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
		AMRG A 1 A_SetBlend("00 00 00", 1.0, 10)
		Goto AltfireContinue
	AltfireReturn:
		AMRG A 1 Offset(-30,82)
		AMRG A 1 Offset(-25,72)
		AMRG A 1 Offset(-20,62)
		AMRG A 1 Offset(-15,52)
		AMRG A 1 Offset(-10,44)
		AMRG A 1 Offset(-5,38)
		AMRG A 1 Offset(-1,35)
		Goto Ready
	Flash:
		TNT1 A 3 A_Light2
		TNT1 A 3 A_Light1
		TNT1 A 0 A_Light0
		Goto LightDone
	}
}